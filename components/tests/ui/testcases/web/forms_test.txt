*** Settings ***
Documentation     Tests submission of forms.

Resource          ../config.txt
Resource          ../../resources/web/login.txt
Resource          ../../resources/web/tree.txt
# Library           KeyClickLibrary

*** Test Cases ***

Test Channel Rename
    [Documentation]     Select User, Project, Dataset, Image and checks
    ...                 whether the toolbar/right-click menu options for
    ...                 creating various containers are enabled.

    User "${USERNAME}" logs in with password "${PASSWORD}"
    Maximize Browser Window

    Tree Should Be Visible
    Wait For Node To Be Visible                 ${FULL NAME}
    Select And Expand Project
    Select And Expand Dataset
    ${imageId}=                                 Select And Expand Image
    Wait Until Page Contains Element            id=editChannelNames
    Click Element                               id=editChannelNames
    Input Text                                  channel0    renameTest
    Submit Form                                 channel_names_edit
    Wait Until Keyword Succeeds                 5 sec   1 sec   Element Should Not Be Visible     channel0
    Page Should Contain                         renameTest


Test Basket Share
    [Documentation]     Tests adding image to basket, creating a share and editing share

    Go To                                       ${WELCOME URL}
    ${imageId}=                                 Select And Expand Image
    Click Element                               basketButton
    Go To                                       ${WELCOME URL}basket/
    Wait Until Page Contains Element            xpath=//table[@id='dataTable']
    Click Element                               xpath=//table[@id='dataTable']//tr/td
    Click Element                               create_share
    Wait Until Page Contains Element            id_message
    Input Text                                  id_message      Created via Selenium Test
    Select All From List                        id_members
    Submit Form                                 create_share_form
    Wait Until Page Contains Element            id=Public
    Wait Until Page Contains Element            xpath=//div[@id='dataTree']//li[@rel='share']/a
    Click Element                               xpath=//div[@id='dataTree']//li[@rel='share']/a
    # Test Editing of Share. NB: Adding a Comment to Share is currently broken.
    Wait Until Page Contains Element            id=edit_share
    Click Element                               id=edit_share
    Wait Until Page Contains Element            id_message
    Input Text                                  id_message      Edited Share Message
    Submit Form                                 edit_share_form
    Wait Until Page Contains                    Edited Share Message


Test Discussion

    Go To                                       ${WELCOME URL}basket/
    Click Element                               id=chatButton
    Wait Until Page Contains Element            create_discussion_form
    # Can't Use IDs for form fields because they are not unique. Page also contains #create_share_form with these fields.
    Wait Until Page Contains Element            css=#create_discussion_form textarea    10
    Input Text                                  css=#create_discussion_form textarea    Created via Selenium Test
    Select All From List                        css=#create_discussion_form select
    Submit Form                                 create_discussion_form
    # Click Element                               css=div.ui-dialog-buttonset:visible button:first
    Wait Until Page Contains Element            xpath=//div[@id='dataTree']//li[@rel='discussion']/a    60
    Click Element                               xpath=//div[@id='dataTree']//li[@rel='discussion']/a
    # Test Editing of Discussion. NB: Adding a Comment to Share is currently broken.
    Wait Until Page Contains Element            id=edit_share
    Click Element                               id=edit_share
    Wait Until Page Contains Element            id_message
    Input Text                                  id_message      Edited Discussion Message
    Submit Form                                 edit_share_form
    Wait Until Page Contains                    Edited Discussion Message       60


Test Annotate
    [Documentation]     Test Annotation of a Project that we create

    Go To                                       ${WELCOME URL}
    Select Experimenter
    ${pid}=                                     Create Project

    # Comment Form
    Input Text                                  comment     test add comment
    Click Button                                Add Comment
    Wait Until Page Contains                    test add comment

    # Tags
    Click Element                               launch_tags_form
    Wait Until Page Contains Element            id_tag
    Input Text                                  id_tag     testSeleniumTag${pid}
    Click Element                               id_add_new_tag
    Click Element                               xpath=//button/span[contains(text(),'Save')]

    # Files - TODO: test scripts first, to generate file


Test Batch Annotate
    [Documentation]     Test Batch Annotation of 2 Projects that we create

    Go To                                       ${WELCOME URL}
    Select Experimenter
    ${projectId}=                               Create Project
    ${pId}=                                     Create Project
    Go To                                       ${WELCOME URL}?show=project-${projectId}|project-${pId}
    Wait Until Page Contains Element            id=batch_ann_title

    # Comment Form
    Input Text                                  comment     test add comment
    Click Button                                Add Comment
    Wait Until Page Contains                    test add comment

    # Tags
    Click Element                               launch_tags_form
    Wait Until Page Contains Element            id_tag
    Input Text                                  id_tag     testSeleniumTag${pid}
    Click Element                               id_add_new_tag
    Click Element                               xpath=//button/span[contains(text(),'Save')]

    # Files - TODO: test scripts first, to generate file

    [Teardown]    Close Browser
