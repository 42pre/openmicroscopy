*** Settings ***
Documentation     Tests ajax 'POST' updates.

Resource          ../../resources/config.txt
Resource          ../../resources/web/login.txt
Resource          ../../resources/web/tree.txt

Suite Setup         Run Keywords  User "${USERNAME}" logs in with password "${PASSWORD}"  Maximize Browser Window
Suite Teardown      Close all browsers

*** Test Cases ***

Test Edit Project
    [Documentation]     Create a Project and edit its name and description

    Select Experimenter
    ${pid}=                                 Create project      robot test edit
    Wait Until Page Contains Element        projectname-${pid}
    # Edit Name
    Click Element                           css=#projectname-${pid} button.btn_edit
    Wait Until Page Contains Element        form-projectname-${pid}
    # id 'id_name' is not unique!
    Input Text                              xpath=//form[@id='form-projectname-${pid}']//input[@id='id_name']  editedName
    Submit Form                             form-projectname-${pid}
    Wait Until Page Contains Element        xpath=//span[@id='projectname-${pid}-name'][contains(text(), 'editedName')]

    # Edit Description
    Click Element                           css=#projectdescription-${pid} button.btn_edit
    Wait Until Page Contains Element        form-projectdescription-${pid}
    Input Text                              xpath=//form[@id='form-projectdescription-${pid}']//textarea[@id='id_description']  newDesc
    Submit Form                             form-projectdescription-${pid}
    Wait Until Page Contains Element        xpath=//span[@id='projectdescription-${pid}-description'][contains(text(), 'newDesc')]


Test Copy Paste Dataset
    [Documentation]     Create 2 Projects and a Dataset. Copy and Paste the Dataset.

    Select Experimenter
    ${p1id}=                                Create project      test copy-paste TO here
    ${p2id}=                                Create project      test copy-paste FROM here
    ${did}=                                 Create Dataset
    Click Element                           refreshButton
    Select Project By Id                    ${p2id}
    Wait Until Page Contains Element        xpath=//li[@data-id='${p2id}']//li[@data-id='${did}']
    Select Dataset By Id                    ${did}
    Click Element                           id=copyButton
    Select Project By Id                    ${p1id}
    Click Element                           id=pasteButton
    # Dataset should now be in BOTH Projects
    # Need to expand the Paste-To Project to reveal Dataset
    Select Project By Id                    ${p1id}
    Wait Until Page Contains Element        xpath=//li[@data-id='${p1id}']//li[@data-id='${did}']
    Wait Until Page Contains Element        xpath=//li[@data-id='${p2id}']//li[@data-id='${did}']


Test Cut Paste Dataset
    [Documentation]     Create 2 Projects and a Dataset. Cut and Paste the Dataset.

    Select Experimenter
    ${p1id}=                                Create project      test Cut-Paste TO here
    ${p2id}=                                Create project      test Cut-Paste FROM here
    ${did}=                                 Create Dataset
    Click Element                           refreshButton
    # Check hierarchy
    Select Project By Id                    ${p2id}
    Wait Until Element Is Visible           xpath=//li[@data-id='${p2id}']/ul/li[@data-id='${did}']     ${WAIT}
    Select Dataset By Id                    ${did}
    # Wait until metadata panel loads
    Wait Until Page Contains                Dataset ID:          30
    Click Element                           id=cutButton
    # POST a /move/ action - wait for Orphaned Dataset
    Wait Until Element Is Visible           xpath=//div[@id='dataTree']/ul/li/ul/li[@data-id='${did}']  ${WAIT}
    Wait Until Element Is Visible           xpath=//li[@id='${treeRootId}']/ul/li[@data-id='${did}']    ${WAIT}
    Select Project By Id                    ${p1id}
    Click Element                           id=pasteButton
    # Another /move/ to different Project - Dataset should NOT be in first Project
    # Need to expand the Paste-To Project to reveal Dataset
    Select Project By Id                    ${p1id}
    Wait Until Element Is Visible           xpath=//li[@data-id='${p1id}']/ul/li[@data-id='${did}']     ${WAIT}
    Page Should Not Contain Element         xpath=//li[@data-id='${p2id}']/ul/li[@data-id='${did}']


Test Cut Paste Image
    [Documentation]     Cut an existing Image to /remove/ it to Orphaned and Paste it back

    Select Experimenter
    ${pId}    ${datasetId}    ${imageId}        Select And Expand Project Dataset Image
    Wait Until Element is Visible               xpath=//li[@data-id='${datasetId}']//li[@data-id='${imageId}']
    Click Element                               id=cutButton
    Wait Until Keyword Succeeds           10    ${INTERVAL}   Page Should Not Contain Element    xpath=//li[@data-id='${datasetId}']//li[@data-id='${imageId}']
    # Dataset should be selected, paste back
    Dataset Should Be Selected By Id            ${datasetId}
    Click Element                               id=pasteButton
    Wait Until Page Contains Element            xpath=//li[@data-id='${datasetId}']//li[@data-id='${imageId}']


Test Chgrp
    [Documentation]     Tests chgrp of a Project to any other group

    # Clear any activities from earlier tests etc.
    Click Element                           id=launch_activities
    Click Element                           id=clear_activities
    Select Experimenter
    ${pid}=                                 Create project      robot test chgrp
    Click Element                           refreshButton
    ${nodeId}=                              Select Project By Id            ${pid}
    Open Context Menu                       xpath=//li[@id='${nodeId}']/a
    Click Element                           xpath=//ul[contains(@class, 'jstree-contextmenu')]//a[contains(text(), 'Move to Group...')]
    Wait Until Element Is Visible           id=group_chooser
    Wait Until Element Is Visible           xpath=//div[@id='group_chooser']/div[contains(@class, 'chgrpGroup')]
    # Simply click first target group option
    Click Element                           xpath=//div[@id='group_chooser']/div[contains(@class, 'chgrpGroup')]
    Submit Form                             id=chgrp-form
    Click Element                           id=launch_activities
    Wait Until Page Contains                Project moved to Group              60

